

plugins {
  id 'maven-publish'
  id 'com.gradle.plugin-publish' version '1.2.1'
  id 'java-gradle-plugin'
  id 'eclipse'
  id 'groovy'
}

group = "edu.sc.seis"
version = "2.3.1"

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
  implementation gradleApi()
  implementation localGroovy() //not needed for Java plugins
  // other dependencies that your plugin requires
}

// taken from https://github.com/crotwell/gradle-macappbundle/pull/74/
rootProject.tasks.named("processResources") {
    duplicatesStrategy = 'include'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

gradlePlugin {
    website = 'https://github.com/crotwell/gradle-macappbundle'
    vcsUrl = 'https://github.com/crotwell/gradle-macappbundle.git'
    plugins {
      macappbundle {
        id = 'edu.sc.seis.macAppBundle'
        displayName = 'MacApp Bundle plugin'
        description = 'A Gradle Plugin to create a Mac OSX .app application based on the project. '
        implementationClass = 'edu.sc.seis.macAppBundle.MacAppBundlePlugin'
        tags.set([ 'macappbundle', 'dmg', 'osx'])
      }
    }
}


